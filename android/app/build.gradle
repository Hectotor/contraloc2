plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize' // Nécessaire pour les objets Parcelable en Kotlin
    id 'com.google.gms.google-services' // Intégration Firebase
    id "dev.flutter.flutter-gradle-plugin" // Plugin Flutter
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    // Désactiver certains avertissements
    lintOptions {
        disable 'Deprecation'
        disable 'Unchecked'
    }
    namespace = "com.contratloc.app"
    compileSdk = 35 // Cible la dernière version du SDK Android
    ndkVersion = "27.0.12077973" // Version NDK utilisée pour les bibliothèques natives

    defaultConfig {
        applicationId = "com.contratloc.app"
        minSdkVersion 23 // Minimum Android 6.0
        targetSdkVersion flutter.targetSdkVersion // Aligné avec Flutter
        versionCode 36
        // Version du code, doit être incrémenté à chaque mise à jour
        versionName "1.1.3" // Version utilisateur
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true // Active le ViewBinding
    }

    signingConfigs {
        release {
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
            storeFile = keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword = keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.release
            minifyEnabled true // Active l'optimisation du code
            shrinkResources true // Supprime les ressources inutilisées
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL' // Ajoute des symboles pour le débogage natif
            }
        }
        debug {
            signingConfig = signingConfigs.release
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation 'com.android.billingclient:billing:5.0.0' // Google Play Billing pour achats in-app
}